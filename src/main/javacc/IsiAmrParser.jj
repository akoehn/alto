/**
 * JavaCC file
 */


options {
  JDK_VERSION = "1.5";
  STATIC = false;
  FORCE_LA_CHECK=true;
}
PARSER_BEGIN(IsiAmrParser)
package de.up.ling.irtg.algebra.graph;

import java.io.ByteArrayInputStream;
import java.util.ArrayList;
import java.util.HashMap;
import java.util.*;
import java.io.*;
import org.jgrapht.*;
import org.jgrapht.graph.*;
import de.up.ling.tree.*;

public class IsiAmrParser {
    private SGraph graph = null;

	public static SGraph parse(Reader reader) throws ParseException {
		IsiAmrParser parser = new IsiAmrParser(new ByteArrayInputStream(new byte[]{}));

                parser.ReInit(reader);

                parser.graph = new SGraph();

                parser.graph();
		return parser.graph;
	}

}
PARSER_END(IsiAmrParser)

SKIP : { " " | "\t" | "\n" | "\r" }

TOKEN :
{
    <NAME: ((["a"-"z", "A"-"Z", "-", "_", "0"-"9", "."])+ )> |
    <EDGE: [":"] (~[" ", "\t", "\n"])*> |
    <DOUBLE_QUOTED_TOKEN: "\"" (~["\""])* "\"">
}

void graph() :
{
    GraphNode g;
    String name;
    List<String> vars = new ArrayList<String>();
}{
    g = node()
}


GraphNode node() :
{
  GraphNode u = null;
  String nodename;
  String nodelabel = null;
  String edgelabel;
  List<String> sources = null;
  GraphNode edgeTarget;  
}
{
  "(" (nodename=token() ("<" sources=tokenList() ">")? ("/" nodelabel=token())?  {
         u = graph.addNode(nodename, nodelabel);
         if( sources != null ) {
             for( String source : sources ) {
               graph.addSource(source, nodename);
             }
         }
       })?
      (edgelabel=edge()  edgeTarget=node() {
         graph.addEdge(u, edgeTarget, edgelabel);
       })*
  ")" { return u; }
  | nodename=token() { 
       if( graph.containsNode(nodename) ) {
        return graph.getNode(nodename);
       } else {
        return graph.addAnonymousNode(nodename);
       }
   }
}


List<String> tokenList() :
{
  List<String> ret = new ArrayList<String>();
  String x;
}
{
   (x = token() { ret.add(x); } ("," x = token() { ret.add(x); })*  ) { return ret; }
}

String token() :
{
    Token tok;
}
{
    tok = <NAME> { return tok.image; }
    | tok = <DOUBLE_QUOTED_TOKEN> { return tok.image.substring(1, tok.image.length()-1); }
}

String edge() :
{
    Token tok;
}{
    tok = <EDGE> { return tok.image.substring(1); }
}
